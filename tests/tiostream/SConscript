# -*- python -*-

Import('env')
env = env.Clone()

bdirs = [ 'dynld', 'core', 'util' ]

env.Append(LIBS = ['boost_unit_test_framework', 'boost_regex', 'nidas_dynld'])
env.Append(LIBPATH = [env.Dir('#/build_x86/build_dynld')],
           CPPPATH = [ "#", "#/nidas" ],
           RPATH = [ env.Dir("#/build_x86/build_dynld").abspath ])

ldlibdirs = [ env.Dir('#/build_x86/build_%s' % d).abspath for d in bdirs ]
env['ENV']['LD_LIBRARY_PATH'] = ":".join(ldlibdirs)

tests = env.Program('tiostream', "tiostream.cc")

runtest = env.Command("xtest", tests, ["$SOURCE.abspath --log_level=all"])
env.Precious(runtest)
AlwaysBuild(runtest)
Alias('test', runtest)

