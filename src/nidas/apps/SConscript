# -*- python -*-
# 2004, Copyright University Corporation for Atmospheric Research

# This SConscript can be run two ways.  Normally it is part of a full build
# of the NIDAS source tree, using variant dirs to build into different
# paths for each machine architecture, and inheriting configuration
# settings and paths from the top-level SConstruct.  The other way builds
# the applications against an existing installation of NIDAS, by specifying
# the installation path in the NIDAS_PATH variable on the scons
# command-line and by running scons with this SConscript as the SConstruct
# file.  So the standalone build command looks like this:
#
#   scons -f SConscript NIDAS_PATH=/opt/nidas
#
# The top few lines of this file setup the Environment accordingly, while
# the rest of the file does not need to change.

from pathlib import Path
import eol_scons
import eol_scons.parseconfig as pc

from SCons.Script import Environment, ARGUMENTS, Split, Export


def check_pkg_config(env, pkg):
    found = pc.ParseConfig(env, 'pkg-config %s --libs --cflags' % (pkg))
    env.PrintProgress("pkg-config found for %s: %s" %
                      (pkg, ["no", "yes"][int(found)]))
    return found


_nidas_apps = {}


def NidasAddApp(env, node):
    name = env.subst("${TARGET.filebase}", target=node)
    _nidas_apps[name] = node


def NidasApp(env, name):
    return _nidas_apps[name]


def NidasProgram(env, target, source):
    "Build a program, install it, and add it to apps."
    target = '$VARIANT_DIR/bin/' + Path(target).name
    node = env.Program(target=target, source=source)
    env.Install('$PREFIX/bin', node)
    env.NidasAddApp(node)
    return node


def nidasprogram(env: Environment):
    """
    Add NidasProgram and related methods to the Environment.
    """
    env.AddMethod(NidasApp)
    env.AddMethod(NidasProgram)
    env.AddMethod(NidasAddApp)


def nidasapps(env: Environment):
    """
    Add nidasprogram and build dependencies for most apps and test programs.
    """
    env.Require(['nidasprogram', 'libnidas_dynld'])


Export('nidasprogram')
Export('nidasapps')


env = Environment(tools=['default'])

arch = env['ARCH']  # empty string for native builds
mach = env.get('MACH')

env.Tool(nidasprogram)


def jsoncpp(env: Environment):
    jsoncpp_enabled = (arch != 'armbe') and check_pkg_config(env, 'jsoncpp')
    if jsoncpp_enabled:
        env.PrintProgress("jsoncpp enabled for data_stats and sync_dump.")
        env.AppendUnique(CPPDEFINES=['NIDAS_JSONCPP_ENABLED=1'])
    else:
        env.PrintProgress("jsoncpp not enabled.")
        env.AppendUnique(CPPDEFINES=['NIDAS_JSONCPP_ENABLED=0'])
    env['JSONCPP_ENABLED'] = jsoncpp_enabled


programs = Split("""
arl-ingest
raf/arinc_out
ck_aout
ck_calfile
ck_xml
data_dump
data_stats
dmd_mmat_vin_limit_test
dsc_a2d_ck
dsm_server
dsm
raf/extract2d
raf/extractDMT
ir104
lidar_vel
merge_verify
nidas_udp_relay/nidas_udp_relay
nidsmerge
n_hdr_util
proj_configs
rserial
sensor_extract
sensor_sim
statsproc
status_listener
sync_dump
sync_server
test_irig
xml_dump
isff/datasets
""")

conf = env.NidasConfigure()
# tee_tty requires timepps.
conf.CheckCHeader(['time.h', 'sys/timepps.h'])
# tee_i2c requires i2c-dev
# linux/i2c-dev.h exists on RedHat systems, but does not contain
# macros such as i2c_smbus_read_byte_data
conf.CheckCHeader('linux/i2c-dev.h')
env = conf.Finish()

# utility apps only need util library
utilenv = env.Clone()
utilenv.Require(['libnidas_util'])

# check for existence of i2c_smbus_read_byte, typically a macro in
# linux/i2c-dev.h
sdconf = env.Configure()
has_i2c = False
if sdconf.CheckFunc('i2c_smbus_read_byte', '#include <linux/i2c-dev.h>', 'C'):
    has_i2c = True
    # check if i2c API has 3(old) or 4(new) arguments in
    # i2c_smbus_read_i2c_block_data
    if not sdconf.CheckLibWithHeader('', 'linux/i2c-dev.h', 'C',
            'i2c_smbus_read_i2c_block_data(0,0,0,0);'):
        env.Append(CPPDEFINES="OLD_I2C_API")
sdconf.Finish()

env.Tool(jsoncpp)
didbenv = env.Clone()
dbconf = didbenv.Configure()
if bool(arch == 'host' and env.get('JSONCPP_ENABLED') and
        check_pkg_config(didbenv, 'libcurl')):
    env.PrintProgress("C++11, jsoncpp, and libcurl found, "
                      "building data_influxdb.")
    didbenv.Append(CCFLAGS='-Wno-effc++')
    didbenv.Require(['libnidas_dynld'])
    didb = didbenv.NidasProgram('data_influxdb', 'data_influxdb.cc')
    didbenv.Alias('data_influxdb', didb)
dbconf.Finish()

# everything else gets everything
env.Require(['libnidas_dynld'])

for prog in programs:
    prog = env.NidasProgram(prog, prog+'.cc')
    env.Alias(prog[0].name, prog)

env.NidasProgram('prep', 'isff/prep.cc')

# Programs which only use libnidas_util.so, not libnidas.
utilenv.NidasProgram('sing', 'sing.cc')
utilenv.NidasProgram('tee_tty', 'tee_tty.cc')

# Only build uio48 if it was enabled for the dynld library.
env.Tool('uio48')
if env.get('HAVE_UIO48'):
    env.NidasProgram('uio48', 'uio48.cc')

# tee_i2c has a read-from-text-file test mode which doesn't
# require the I2C API, so always build it
env.NidasProgram('tee_i2c', 'tee_i2c.cc')

# ublox requires the I2C API
if has_i2c:
    env.Alias('ublox', utilenv.NidasProgram('ublox', 'ublox.cc'))

utilenv.NidasProgram('utime', 'utime.cc')

if mach == 'viper':
    # controls DIO pins on a Viper
    env.NidasProgram('viper_dio', 'viper_dio.cc')

if arch != 'host':
    # Programs which just do ioctls to NIDAS driver modules, and don't
    # otherwise use anything in the nidas libraries.
    utils = ['set_pcmcom8.c', 'serial_loopback_test.c',
             'isff/set_emerald.c', 'isff/emerald_dio.cc',
             'gps_nmea_sysclock.cc', 'garmin.cc']
    for prog in utils:
        utilenv.NidasProgram(prog, prog+'.cc')

env.Alias('install', ['${INSTALL_ROOT}$PREFIX/bin'])

env.Default("$VARIANT_DIR/bin/.")
