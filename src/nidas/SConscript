# -*- python -*-
## 2006, Copyright University Corporation for Atmospheric Research

import os
from SCons.Script import Environment, Import, SConscript, Export

Import('env')
env: Environment

# -rdynamic: Export all dynamic symbols in the executables, so that libraries
# loaded with dlopen() can resolve symbols already in the executable.
# See "man dlopen" for more info.
env.AppendUnique(LINKFLAGS=['-rdynamic'])

installs = []
installs += env.Install('$ARCHPREFIX/include/nidas','include/nidas/Config.h')

# Build Revision.h from git describe.  This does not depend on the target,
# only the source repo, so it is not installed in the variant dir but in the
# source tree, where it can be included easily in source archives.
env.Tool('gitinfo')
info = env.GitInfo("#/nidas/Revision.h", "#/nidas")
installs += env.Install('$ARCHPREFIX/include/nidas', info)

env.PrependUnique(CPPPATH=['$VARIANT_DIR/include'])

subdirs = env.Split("""util core dynld apps scripts linux""")

for dir in subdirs:
    SConscript('%s/SConscript' % (dir), exports=['env'])

env.Alias('lib', subdirs[0:3])

installs += ['${INSTALL_ROOT}$ARCHPREFIX/include',
             '${INSTALL_ROOT}$ARCHPREFIX/$ARCHLIBDIR']

env.Alias('install', installs)

# this needs to be moved into a tool at some point
ldconfname = 'nidas.conf'
osid = env.get('VARIANT_OS', '')
multiarch = os.environ.get('DEB_HOST_MULTIARCH')
multiarch = multiarch or os.environ.get('DEB_HOST_GNU_TYPE')
debian = osid.startswith('debian') or osid.startswith('raspbian')
print(f'debian={debian}, multiarch={multiarch}')
if debian and multiarch:
    ldconfname = 'nidas-%s.conf' % (multiarch)

ldc = env.Substfile(ldconfname, 'nidas.conf.in')
etcldc = env.Install('${SYSCONFIGDIR}/ld.so.conf.d', ldc)
env.Alias('install.root', etcldc)

# Build nidas.pc.  This substitutes PREFIX as the intended installation
# location, even if everything is being installed under INSTALL_ROOT.  It gets
# installed two places: the nidas lib directory and the system pkg-config
# directory, using two separate aliases, install and install.root.
env['SUBST_DICT'] = {
    '@PREFIX@': '$PREFIX',
    '@ARCHLIBDIR@': '$ARCHLIBDIR',
    '@ARCHPREFIX@': '$ARCHPREFIX',
    '@REPO_TAG@': '$REPO_TAG'
}
pc = env.Substfile('nidas.pc.in')
env.Alias('install', env.Install('$ARCHPREFIX/$ARCHLIBDIR/pkgconfig', pc))
env.Alias('install.root', env.Install('${PKGCONFIGDIR}', pc))
