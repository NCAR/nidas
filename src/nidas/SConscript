# -*- python -*-
## 2006, Copyright University Corporation for Atmospheric Research

from SCons.Script import Environment, Import, SConscript, Export

Import('env')
env: Environment

# -rdynamic: Export all dynamic symbols in the executables, so that libraries
# loaded with dlopen() can resolve symbols already in the executable.
# See "man dlopen" for more info.
env.AppendUnique(LINKFLAGS=['-rdynamic'])

installs = []
installs += env.Install('$ARCHPREFIX/include/nidas','include/nidas/Config.h')

# Update Revision.h from git describe
env.Tool('gitinfo')
info = env.GitInfo("include/nidas/Revision.h", "#/nidas")
installs += env.Install('$ARCHPREFIX/include/nidas', info)

env.PrependUnique(CPPPATH=['$VARIANT_DIR/include'])

subdirs = env.Split("""util core dynld apps linux""")

for dir in subdirs:
    SConscript('%s/SConscript' % (dir), exports=['env'])

env.Alias('lib', subdirs[0:3])

installs += ['$ARCHPREFIX/include', '$ARCHPREFIX/$ARCHLIBDIR']

env.Alias('install', installs)
