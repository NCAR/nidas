# -*- python -*-

##  Copyright 2005,2006 UCAR, NCAR, All Rights Reserved

##
##  Import the build environment.
##
Import('env')

# The following settings modify this environment according to the
# architecture being built, as specified in the ARCH construction
# variable.  
prefix = env['PREFIX'] + '/' + env['ARCH']
env.Replace(PREFIX = prefix)

##
## Specify RPATH to avoid the need for LD_LIBRARY_PATH later 
##
# Note that the addition of "build_core" to LIBPATH actually causes the
# addition of FOUR -L options to the linker commands.  It appears scons
# parses the name in multiple places, recognizes it as a build directory,
# then adds a -L argument for every combination of build and source
# directories with build_core in it.  Maybe it's a bug in scons, or some
# deep mystery.  Anyway, this is avoided by putting a directory node for
# build_core in the path instead of a string.
#
env.PrependUnique(LIBPATH = [env.Dir("build_core")])
env.PrependUnique(RPATH = [env.Dir("build_core").get_abspath()])


subdirs=Split("""util core dynld apps rtlinux linux""")
defaultdirs=subdirs[1:]
libdirs=subdirs[1:3]

##
## ARCH target for creating modules, library, and executables
##
arch = env['ARCH']
env.Alias(arch, ['build_%s' % dir for dir in defaultdirs])
Alias('lib', ['build_%s' % d for d in libdirs])

# Provide an arch-specific install alias
archinstall= env.Alias(arch+'_install', [
	'$PREFIX/include/nidas/util',
	'$PREFIX/include/nidas/core',
	'$PREFIX/include/nidas/dynld',
	'$PREFIX/include/nidas/rtlinux',
	'$PREFIX/include/nidas/linux',
	'$PREFIX/lib',
	'$PREFIX/bin',
        ])

conf = Configure(env)
env = conf.Finish()

if env.has_key('XMLDEBUG') and env['XMLDEBUG']:
    env.AppendUnique(CPPDEFINES = ['XML_DEBUG'])

if env.has_key('GDBDEBUG') and env['GDBDEBUG']:
    env.AppendUnique(CXXFLAGS = ['-ggdb'])

for dir in subdirs:
    SConscript('%s/SConscript' % dir,
               build_dir='build_%s' % dir,
               duplicate=0,exports={'env':env})

Default([ "build_%s" % dir for dir in defaultdirs ])
