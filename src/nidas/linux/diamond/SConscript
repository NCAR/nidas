# -*- python -*-
## 2007, Copyright University Corporation for Atmospheric Research


##
##  Import the build environment.
##
Import(['env', 'first_mach'])
mach = env['MACH']

# The Makefile must be listed in the source dependencies
# so that it is copied/linked to the variant_dir.
# The Kmake builder runs the make command in variant_dir.
#
# Also, the SConscript command that invokes this SConscript
# file should set duplicate=1, so that all the linux module
# source files, including the Makefile, are copied to the variant_dir.
# so that make can find them and do its work outside of the
# source tree.

# All modules must be built by the same kmake builder so SCons will setup
# all the variant_dir copies/links before running make.  Otherwise make
# fails because not all the dependencies exist yet.
#
if env.has_key("LINUX_MODULES_INST_PATH") and \
    env.has_key('KERNELDIR') and env['KERNELDIR'] != '':

    revinfo = Dir('./../../include/nidas/linux').File('Revision.h')
    utilsyms = Dir('./../util_' + mach).File('Module.symvers')
    filtsyms = Dir('./../filters_' + mach).File('Module.symvers')
    irigsyms = Dir('./../irig_' + mach).File('Module.symvers')

    modules = ['emerald.ko', 'dmd_mmat.ko','gpio_mm.ko','ir104.ko']
    env.Kmake(modules,
              ['emerald.c', 'emerald.h', 'dmd_mmat.c', 'dmd_mmat.h',
               'gpio_mm.c', 'gpio_mm.h', 'gpio_mm_regs.h', 'ir104.c', 'ir104.h',
               'Makefile', revinfo, utilsyms, filtsyms, irigsyms])

    env.Install('$LINUX_MODULES_INST_PATH',modules)

    env.Clean('install',[
        '$LINUX_MODULES_INST_PATH/' + l for l in modules
        ])

# Some kernel module header files define symbols that are needed
# by user code. These must also be installed in $ARCHPREFIX/include
headers = Split("""
    emerald.h
    dmd_mmat.h
    gpio_mm.h
    ir104.h
""")

if first_mach:
    env.Install('$ARCHPREFIX/include/nidas/linux/diamond',headers)
