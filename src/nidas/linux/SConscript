# -*- python -*-

##  Copyright 2005,2006 UCAR, NCAR, All Rights Reserved

import os

##
##  Import the build environment.
##
Import('env')
arch = env['ARCH']  # empty string for native builds
machs = env['MACHS']

headers = Split("""
    a2d.h
    isa_bus.h
    irigclock.h
    klog.h
    mesa.h
    ncar_a2d.h
    types.h
    util.h
    #/nidas/linux/SvnInfo.h
""")

subdirs=Split("""
    arinc
    diamond
    filters
    irig
    lams
    mesa
    ncar_a2d
    serial
    viper
    usbtwod
    util
""")

#
# Invoke SConscripts in subdirs to create linux modules.
#
# A SConscript may be invoked more than once, to build
# modules for multiple machines of an architecture.
# Pass a boolean variable 'first_mach' to the SConscript,
# with a value of True the first time the SConscript is
# called for a given architecture, and False for successive times.
# Then the first time is it called, a SConscript can then
# create dependencies for things that are independent
# of machine type for an architecture. This then avoids warnings:
# "warning: Two different environments were specified for target ..."

first_mach = True
vdirs = []
for mach in machs:
    kdir = ''
    if env.has_key('KERNELDIR_' + mach):
        kdir = env['KERNELDIR_' + mach]
    elif env.has_key('KERNELDIR'):
        kdir = env['KERNELDIR']

    if kdir != '' and not os.path.exists(kdir):
        print "KERNELDIR_" + mach + "=" + kdir + " not found. Skipping module builds"
        continue

    for dir in subdirs:
        vdir ='build_%s_%s' % (dir,mach)
        # If LINUX_MODULES is undefined, or is True, then the
        # user wants to build the linux modules. If so,
        # redefine it to be where the modules are to be installed.
        if not env.has_key("LINUX_MODULES") or env['LINUX_MODULES']:
            # module installation path must be unique for each machine type
            # For the native host, mach is a null string, and
            # modpath is then simply $PREFIX/modules
            modpath='$PREFIX/modules/' + mach
            mach_env = env.Clone(MACH=mach,KERNELDIR=kdir,LINUX_MODULES=modpath)
        else:
            mach_env = env.Clone(MACH=mach,KERNELDIR=kdir)

        mach_env.SConscript('%s/SConscript' % dir,
                   variant_dir=vdir,
                   duplicate=1,exports=['first_mach',{'env':mach_env}])

        # On native host, install the headers
        if arch == '':
            env.Install('$PREFIX/include/nidas/linux', headers)

        vdirs = [vdirs,vdir]
        first_mach = False

# if arch != '':
#     Alias(arch,vdirs)
