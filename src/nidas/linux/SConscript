# -*- python -*-

##  Copyright 2005,2006 UCAR, NCAR, All Rights Reserved

##
##  Import the build environment.
##
Import('env')

# Make a copy of the environment so that we can add $KERNELDIR/include
# to CPPPATH without effecting builds in other directories.
myenv = env.Copy()

arch = myenv['ARCH']

myenv.AppendUnique(CPPPATH = [myenv.subst("$KERNELDIR/include")] )

subdirs=Split("""
    diamond
    filters
    irig
    mesa
    ncar_a2d
    serial
    usbtwod
    util
""")

if arch == 'arm':
    archdirs=Split("""
        viper
    """)
else:
    archdirs=Split("""
    """)

##
## ARCH target for creating modules, library, and executables
##
build_dirs = ['build_%s' % dir for dir in subdirs + archdirs]
myenv.Alias(arch, build_dirs)

# Provide an arch-specific install alias.  Basically, install everything
# that goes under the arch-specific install prefix.
archinstall= myenv.Alias(arch+'_install', [ '$PREFIX' ])

for dir in subdirs + archdirs:
    myenv.SConscript('%s/SConscript' % dir,
               build_dir='build_%s' % dir,
               duplicate=1,exports={'env':myenv})

Default(build_dirs)

headers = Split("""
    a2d.h
    isa_bus.h
    irigclock.h
    klog.h
    mesa.h
    ncar_a2d.h
    types.h
    util.h
""")

myenv.Install('$PREFIX/include/nidas/linux', headers)
