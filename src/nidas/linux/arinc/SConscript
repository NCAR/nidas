# -*- python -*-

##  Copyright 2005,2006 UCAR, NCAR, All Rights Reserved

##
##  Import the build environment.
##
Import('env')
arch = env['ARCH']

# The Makefile must be listed in the source dependencies
# so that it is copied/linked to the variant_dir.
# The Kmake builder runs the make command in variant_dir.
#
# For some reason some versions of scons fail with an error:
# scons: *** 'NoneType' object has no attribute 'select'
# if the Makefile is the first source file.
#
# Also, the SConscript command that invokes this SConscript
# file should set duplicate=1, so that all the linux module
# source files, including the Makefile, are copied to the variant_dir.
# so that make can find them and do its work outside of the
# source tree.

arinc_files = Split("""
    main.c
    arinc.h
    CEI420A/Source/api220.c
    CEI420A/Source/api220.h
    CEI420A/Source/apiutils.c
    CEI420A/Source/arincx20.h
    CEI420A/Source/lowlevel.h
    CEI420A/Source/fpga420a_16mhz_v100.h
    CEI420A/Source/i960_220.h
    CEI420A/Include/utildefs.h
    Makefile
""")

if ((not env.has_key("LINUX_MODULES") or env['LINUX_MODULES']) and
    env.has_key('KERNELDIR') and env['KERNELDIR'] != ''):
    modules = ['arinc.ko']
    env.Kmake(modules,arinc_files)

    env.Install('$PREFIX/linux',modules)

    # print [ env.subst('$PREFIX/linux/') + l for l in modules ]
    # env.Clean(arch + '_install',[
    #     '$PREFIX/linux/' + l for l in modules
    #     ])
    env.Clean('install',[
        '$PREFIX/linux/' + l for l in modules
        ])

# Some kernel module header files define symbols that are needed
# by user code. These must also be installed in $PREFIX/include
headers = Split("""
    arinc.h
""")

env.Install('$PREFIX/include/nidas/linux/arinc',headers)
