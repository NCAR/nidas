# -*- python -*-
## 2006, Copyright University Corporation for Atmospheric Research


##
##  Import the build environment.
##
Import('env')
env = env.Clone()
arch = env['ARCH']  # empty string for native builds
targetArch=arch
if targetArch == "":
    targetArch="host"

# env.AppendUnique(CXXFLAGS = Split("""
# """))

##
##  List of headers files
##
headers = [ Split("""
    auto_ptr.h
    BitArray.h
    BluetoothAddress.h
    BluetoothRFCommSocket.h
    BluetoothRFCommSocketAddress.h
    Bzip2FileSet.h
    DatagramPacket.h
    DSMPowerCtrl.h
    DSMSwitchCtrl.h
    EndianConverter.h
    EOFException.h
    Exception.h
    FileSet.h
    FtdiHW.h
    Inet4Address.h
    Inet4NetworkInterface.h
    Inet4PacketInfo.h
    Inet4SocketAddress.h
    InterruptedException.h
    InvalidParameterException.h
    IOException.h
    IOTimeoutException.h
    IoPollerIf.h
    IoPollerAbs.h
    Logger.h
    McSocket.h
    MutexCount.h
    ParseException.h
    PowerCtrlIf.h
    PowerCtrlAbs.h
    FtdiDSMPowerCtrl.h
    FtdiDSMPowerGPIO.h
    FtdiSensorPowerCtrl.h
    FtdiSwitchGPIO.h
    PPoll.h
    Process.h
    ptytools.h
    SPoll.h
    SensorPowerCtrl.h
    SerialOptions.h
    SerialPort.h
    SocketAddress.h
    Socket.h
    SysfsGpio.h
    SysfsDSMPowerCtrl.h
    SysfsSensorPowerCtrl.h
    Termios.h
    Thread.h
    ThreadSupport.h
    time_constants.h
    UnixSocketAddress.h
    UnknownHostException.h
    UTime.h
    util.h
    FtdiXcvrGPIO.h
    """)
]
# print(["headers="] + [str(h) for h in headers])

sources = [ Split("""
    BitArray.cc
    BluetoothAddress.cc
    BluetoothRFCommSocket.cc
    BluetoothRFCommSocketAddress.cc
    Bzip2FileSet.cc
    DSMPowerCtrl.cc
    EndianConverter.cc
    Exception.cc
    FileSet.cc
    FtdiHW.cc
    FtdiDSMPowerCtrl.cc
    FtdiSensorPowerCtrl.cc
    Inet4Address.cc
    Inet4NetworkInterface.cc
    Inet4SocketAddress.cc
    Logger.cc
    McSocket.cc
    PPoll.cc
    PowerCtrlAbs.cc
    Process.cc
    ptytools.cc
    SPoll.cc
    SensorPowerCtrl.cc
    SerialOptions.cc
    SerialPort.cc
    Socket.cc
    SysfsGpio.cc
    SysfsDSMPowerCtrl.cc
    SysfsSensorPowerCtrl.cc
    Termios.cc
    Thread.cc
    ThreadSupport.cc
    UnixSocketAddress.cc
    UTime.cc
    util.cc
    """)
]
# print(["sources="] + [str(h) for h in sources])

objects = env.SharedObject(sources)

##
##  Build the libnidas_util library.
##
conf = Configure(env)
if conf.CheckLib('cap'):
    conf.env.AppendUnique(LIBS = 'cap')
if conf.CheckLib('bz2'):
    conf.env.AppendUnique(LIBS = 'bz2')
if conf.CheckLib('bluetooth'):
    conf.env.AppendUnique(LIBS = 'bluetooth')
if conf.CheckLib('usb-1.0'):
    conf.env.AppendUnique(LIBS = 'usb-1.0')
if conf.CheckLib('boost_regex'):
    conf.env.AppendUnique(LIBS = 'boost_regex')
if conf.CheckLib('boost_system'):
    conf.env.AppendUnique(LIBS = 'boost_system')
if conf.CheckLib('boost_filesystem'):
    conf.env.AppendUnique(LIBS = 'boost_filesystem')
env = conf.Finish()

# prepend rel path w/# to escape the confines of scons root directory w/SConstruct.
libftdiRelPath = File('#../../libftdi/libftdi1-1.4/build-' + targetArch + '/src/libftdi1.a')

# build libnidas_util.so
lib = env.SharedLibrary3('nidas_util' , objects,
                         LIBS= ['pthread'] + [libftdiRelPath] + env['LIBS'])
Depends(lib, libftdiRelPath)

# Test SConscripts need the full path to the library to set LD_LIBRARY_PATH
Export({'LIBNIDAS_UTIL' + arch: lib[0]})

slib = env.StaticLibrary('nidas_util' , objects)

##
## Install targets for libraries and headers.
##
nodes = []
nodes += env.SharedLibrary3Install('$ARCHPREFIX',lib)
nodes += env.Install('$ARCHPREFIX/$ARCHLIBDIR',slib)
nodes += env.Install('$ARCHPREFIX/include/nidas/util',headers)

# Build nidas.pc from the top level. It is done here since we know
# ARCHLIBDIR and REPO_TAG
env.Command('nidas.pc', '#nidas.pc.in',
            "sed -e 's,@PREFIX@,$ARCHPREFIX,' "
            "-e 's,@ARCHLIBDIR@,$ARCHLIBDIR,' "
            "-e 's,@REPO_TAG@,$REPO_TAG,' < $SOURCE > $TARGET")

Alias('install', env.Install('$ARCHPREFIX/$ARCHLIBDIR/pkgconfig', 'nidas.pc'))

Alias('install',nodes)
env.Clean('install', nodes)
