# -*- python -*-
## 2006, Copyright University Corporation for Atmospheric Research


##
##  Import the build environment.
##
Import('env')
env = env.Clone()
arch = env['ARCH']  # empty string for native builds

# env.AppendUnique(CXXFLAGS = Split("""
# """))

##
##  List of headers files
##
headers = [ Split("""
    auto_ptr.h
    AutoConfigHW.h
    BitArray.h
    BluetoothAddress.h
    BluetoothRFCommSocket.h
    BluetoothRFCommSocketAddress.h
    Bzip2FileSet.h
    DatagramPacket.h
    EndianConverter.h
    EOFException.h
    Exception.h
    FileSet.h
    FtdiHW.h
    Inet4Address.h
    Inet4NetworkInterface.h
    Inet4PacketInfo.h
    Inet4SocketAddress.h
    InterruptedException.h
    InvalidParameterException.h
    IOException.h
    IOTimeoutException.h
    Logger.h
    McSocket.h
    MutexCount.h
    ParseException.h
    Process.h
    SensorPowerCtrl.h
    SerialOptions.h
    SerialPort.h
    SocketAddress.h
    Socket.h
    Termios.h
    Thread.h
    ThreadSupport.h
    time_constants.h
    UnixSocketAddress.h
    UnknownHostException.h
    UTime.h
    util.h
    """)
]
# print(["headers="] + [str(h) for h in headers])

sources = [ Split("""
    AutoConfigHW.cc
    BitArray.cc
    BluetoothAddress.cc
    BluetoothRFCommSocket.cc
    BluetoothRFCommSocketAddress.cc
    Bzip2FileSet.cc
    EndianConverter.cc
    Exception.cc
    FileSet.cc
    FtdiHW.cc
    Inet4Address.cc
    Inet4NetworkInterface.cc
    Inet4SocketAddress.cc
    Logger.cc
    McSocket.cc
    Process.cc
    SensorPowerCtrl.cc
    SerialOptions.cc
    SerialPort.cc
    Socket.cc
    Termios.cc
    Thread.cc
    ThreadSupport.cc
    UnixSocketAddress.cc
    UTime.cc
    util.cc
    """)
]
# print(["sources="] + [str(h) for h in sources])

objects = env.SharedObject(sources)

##
##  Build the libnidas_util library.
##
conf = Configure(env)
if conf.CheckLib('cap'):
    conf.env.AppendUnique(LIBS = 'cap')
if conf.CheckLib('bz2'):
    conf.env.AppendUnique(LIBS = 'bz2')
if conf.CheckLib('bluetooth'):
    conf.env.AppendUnique(LIBS = 'bluetooth')
env = conf.Finish()

# build libnidas_util.so
lib = env.SharedLibrary3('nidas_util' , objects,
    LIBS= ['pthread'] + env['LIBS'] + ['usb-1.0', 'ftdi1'])

# Test SConscripts need the full path to the library to set LD_LIBRARY_PATH
Export({'LIBNIDAS_UTIL' + arch: lib[0]})

slib = env.StaticLibrary('nidas_util' , objects)

##
## Install targets for libraries and headers.
##
nodes = []
nodes += env.SharedLibrary3Install('$PREFIX',lib)
nodes += env.Install('$PREFIX/$ARCHLIBDIR',slib)
nodes += env.Install('$PREFIX/include/nidas/util',headers)

# Build nidas.pc from the top level. It is done here since we know
# ARCHLIBDIR and REPO_TAG
env.Command('nidas.pc', '#nidas.pc.in',
    "sed -e 's,@PREFIX@,$PREFIX,' -e 's,@ARCHLIBDIR@,$ARCHLIBDIR,' -e 's,@REPO_TAG@,$REPO_TAG,' < $SOURCE > $TARGET")

Alias('install', env.Install('$PREFIX/$ARCHLIBDIR/pkgconfig', 'nidas.pc'))

Alias('install',nodes)
env.Clean('install', nodes)
