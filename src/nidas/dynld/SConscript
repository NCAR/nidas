# -*- python -*-

##  Copyright 2005 UCAR, NCAR, All Rights Reserved

import glob
import fnmatch
import os

##
##  Import the cross-build environment.
##
Import('env')
# env = env.Clone()
arch = env['ARCH']

conf = Configure(env)

# Check for nc_server header and library on /opt/nc_server
libpath = env['LIBPATH']

# arch-dependent library path
if (arch == 'x86'):
    nclibpath = '/opt/nc_server/lib'
else:
    nclibpath =  '/opt/nc_server/' + arch + '/lib'

env.Append(LIBPATH=[nclibpath])

if conf.CheckLibWithHeader('nc_server_rpc',
    '/opt/nc_server/include/nc_server_rpc.h','C++',
    'xdr_connection(0,0);',autoadd=1):
    conf.env.AppendUnique(
        CPPDEFINES = ['HAS_NC_SERVER_RPC_H'],
        CPPPATH=['/opt/nc_server/include'],
        LIBS=['nc_server_rpc'])
else:
    conf.env.Replace(LIBPATH=[libpath])
env = conf.Finish()

# -ggdb
# -DXML_DEBUG
# env.AppendUnique(CXXFLAGS = Split("""
# """))

SConscript(dirs = ["raf", "isff", "iss"], exports={'env':env})

headers = [ Split("""
    A2DSensor.h
    AsciiOutput.h
    Bzip2FileSet.h
    DSC_A2DSensor.h
    DSC_AnalogOut.h
    DSC_Event.h
    DSC_FreqCounter.h
    DSC_PulseCounter.h
    DSMSerialSensor.h
    FileSet.h
    GPS_NMEA_Process.h
    GPS_NMEA_Serial.h
    GPS_NMEA_NetSensor.h
    ParoSci_202BG_Calibration.h
    ParoSci_202BG_P.h
    ParoSci_202BG_T.h
    RawSampleInputStream.h
    RawSampleOutputStream.h
    RawSampleService.h
    SampleArchiver.h
    SampleInputStream.h
    SampleOutputStream.h
    SampleProcessor.h
    StatisticsCruncher.h
    StatisticsProcessor.h
    TSI_CPC3772.h
    UDPSampleOutput.h
    UDPSocketSensor.h
    ViperDIO.h
    WatchedFileSensor.h
    WxtSensor.h
    XMLConfigService.h
""") ]

##
##  source files in libnidas_dynld
##
sources = [ Split("""
    A2DSensor.cc
    AsciiOutput.cc
    Bzip2FileSet.cc
    DSC_A2DSensor.cc
    DSC_AnalogOut.cc
    DSC_Event.cc
    DSC_FreqCounter.cc
    DSC_PulseCounter.cc
    DSMSerialSensor.cc
    GPS_NMEA_Process.cc
    GPS_NMEA_Serial.cc
    GPS_NMEA_NetSensor.cc
    FileSet.cc
    ParoSci_202BG_Calibration.cc
    ParoSci_202BG_P.cc
    ParoSci_202BG_T.cc
    RawSampleInputStream.cc
    RawSampleOutputStream.cc
    RawSampleService.cc
    SampleArchiver.cc
    SampleInputStream.cc
    SampleOutputStream.cc
    SampleProcessor.cc
    StatisticsCruncher.cc
    StatisticsProcessor.cc
    TSI_CPC3772.cc
    UDPSampleOutput.cc
    UDPSocketSensor.cc
    ViperDIO.cc
    WatchedFileSensor.cc
    WxtSensor.cc
    XMLConfigService.cc
""") ]

if arch == 'arm':
    archsources=Split("""
    """)
    archheaders=Split("""
    """)
else:
    archsources=Split("""
    """)
    archheaders=Split("""
    """)

SConscript(dirs="psql", duplicate=0, exports={'env':env})

##
##  Build the nidas_dynld library.
##
env.Append (LIBNIDAS_DYNLD_SOURCES = sources + archsources )

# build libnidas_dynld.so
lib = env.SharedLibrary3('nidas_dynld' , env['LIBNIDAS_DYNLD_SOURCES'],
    LIBS=['nidas'] + env['LIBS'],
    LIBPATH = [env.Dir('../build_core')] + env['LIBPATH'])

##
## Install targets for library and headers.
##
nodes = []
nodes += env.SharedLibrary3Install('$PREFIX',lib)
nodes += env.Install('$PREFIX/include/nidas/dynld',headers + archheaders)
Alias('install',nodes)

# print [ env.subst('$PREFIX/lib/') + os.path.basename(File(l).path) for l in lib]

# env.Clean(arch + '_install', [
#     ('$PREFIX/lib/' + os.path.basename(File(l).path) for l in lib)
#     ])
env.Clean('install',nodes)

env['LIBNIDAS_DYNLD'] = lib[0]
