# -*- python -*-
## 2004, Copyright University Corporation for Atmospheric Research


##
##  Import the build environment.
##
Import('env')
env = env.Clone(tools = ['nidas'])
arch = env['ARCH']  # empty string for native builds

##
##  List of header files
##
headers = Split("""
    AdaptiveDespiker.h
    AsciiSscanf.h
    BluetoothRFCommSocketIODevice.h
    Bzip2FileSet.h
    CalFile.h
    CharacterSensor.h
    ConnectionInfo.h
    ConnectionRequester.h
    Datagrams.h
    DatagramSocket.h
    Datasets.h
    DerivedDataClient.h
    DerivedDataReader.h
    Dictionary.h
    DOMable.h
    DOMObjectFactory.h
    DSMCatalog.h
    DSMConfig.h
    DSMEngine.h
    DSMEngineIntf.h
    DSMSensor.h
    DSMServer.h
    DSMServerApp.h
    DSMServerIntf.h
    DSMService.h
    DynamicLoader.h
    FileSet.h
    FsMount.h
    HeaderSource.h
    IOChannel.h
    IODevice.h
    IOStream.h
    LooperClient.h
    Looper.h
    McSocket.h
    McSocketUDP.h
    MultipleUDPSockets.h
    NearestResampler.h
    NearestResamplerAtRate.h
    NidasApp.h
    NidsIterators.h
    Parameter.h
    PhysConstants.h
    Polled.h
    Project.h
    ProjectConfigs.h
    Prompt.h
    RemoteSerialConnection.h
    RemoteSerialListener.h
    Resampler.h
    SampleArchiver.h
    SampleAverager.h
    SampleClient.h
    SampleClientList.h
    SampleClock.h
    Sample.h
    SampleInput.h
    SampleInputHeader.h
    SampleIOProcessor.h
    SampleLengthException.h
    SampleMatcher.h
    SampleOutput.h
    SampleOutputRequestThread.h
    SampleParseException.h
    SamplePipeline.h
    SamplePool.h
    SampleBuffer.h
    SampleScanner.h
    SampleSorter.h
    SampleSource.h
    SampleSourceSupport.h
    SampleStats.h
    SampleTag.h
    SampleThread.h
    SensorCatalog.h
    SensorHandler.h
    SensorOpener.h
    SerialPortIODevice.h
    SerialSensor.h
    ServiceCatalog.h
    Site.h
    Socket.h
    SocketAddrs.h
    SocketIODevice.h
    TCPSocketIODevice.h
    UDPSocketIODevice.h
    ServerSocketIODevice.h
    SortedSampleSet.h
    StatusHandler.h
    StatusListener.h
    StatusThread.h
    UnixIOChannel.h
    UnixIODevice.h
    Variable.h
    VariableConverter.h
    Version.h
    XDOM.h
    requestXMLConfig.h
    XMLConfigInput.h
    XMLConfigWriter.h
    XMLException.h
    XMLFdBinInputStream.h
    XMLFdFormatTarget.h
    XMLFdInputSource.h
    XMLParser.h
    XMLWriter.h
    XmlRpcThread.h
    XMLStringConverter.h
""")

# print(["headers="] + [str(h) for h in headers])

sources = Split("""
    AdaptiveDespiker.cc
    BluetoothRFCommSocketIODevice.cc
    Bzip2FileSet.cc
    CalFile.cc
    CharacterSensor.cc
    DatagramSocket.cc
    Datasets.cc
    DerivedDataReader.cc
    Dictionary.cc
    DOMable.cc
    DOMObjectFactory.cc
    DSMCatalog.cc
    DSMConfig.cc
    DSMEngine.cc
    DSMEngineIntf.cc
    DSMSensor.cc
    DSMServer.cc
    DSMServerApp.cc
    DSMServerIntf.cc
    DSMService.cc
    DynamicLoader.cc
    FileSet.cc
    FsMount.cc
    HeaderSource.cc
    IOChannel.cc
    IOStream.cc
    Looper.cc
    McSocket.cc
    McSocketUDP.cc
    MultipleUDPSockets.cc
    NearestResampler.cc
    NearestResamplerAtRate.cc
    NidasApp.cc
    NidsIterators.cc
    Parameter.cc
    Project.cc
    ProjectConfigs.cc
    RemoteSerialConnection.cc
    RemoteSerialListener.cc
    SampleArchiver.cc
    SampleAverager.cc
    SampleClientList.cc
    SampleClock.cc
    SampleInputHeader.cc
    SampleIOProcessor.cc
    SampleMatcher.cc
    SampleOutput.cc
    SampleOutputRequestThread.cc
    SamplePipeline.cc
    SamplePool.cc
    SampleBuffer.cc
    SampleScanner.cc
    SampleSorter.cc
    SampleSourceSupport.cc
    SampleTag.cc
    SensorCatalog.cc
    SensorHandler.cc
    SensorOpener.cc
    SerialPortIODevice.cc
    SerialSensor.cc
    ServiceCatalog.cc
    Site.cc
    Socket.cc
    SocketIODevice.cc
    TCPSocketIODevice.cc
    UDPSocketIODevice.cc
    ServerSocketIODevice.cc
    StatusHandler.cc
    StatusListener.cc
    StatusThread.cc
    UnixIOChannel.cc
    UnixIODevice.cc
    Variable.cc
    VariableConverter.cc
    Version.cc
    requestXMLConfig.cc
    XMLConfigWriter.cc
    XMLException.cc
    XMLFdFormatTarget.cc
    XMLParser.cc
    XMLWriter.cc
    XmlRpcThread.cc
""")

# If the lex tool is not available, SCons just quits with a
# mysterioius error about "no attribute 'CXXFile'", so check for the
# tool explicitly and print a more useful error message.
if 'lex' not in env['TOOLS']:
    import SCons.Errors
    raise SCons.Errors.StopError("Flex (or some lex tool) must be installed.")

# Create AsciiSscanf.cc from the .ll lex source.
env.CXXFile(target='AsciiSscanf.cc',source='AsciiSscanf.ll')

# Override CXXFLAGS when compiling AsciiSscanf.cc to turn off -Weffc++ warnings.
# Code generated by flex generates many warnigs.
aobj = env.SharedObject('AsciiSscanf.cc',CXXFLAGS="-Wall -O2")

if env.get('ENABLE_VALGRIND'):
    sampleobj = env.SharedObject('Sample.cc', 
                                 CPPDEFINES = env.get('CPPDEFINES', []) +
                                 ['ENABLE_VALGRIND'])
else:
    sampleobj = env.SharedObject('Sample.cc')

##
##  Build the libnidas library. Search other needed libraries.
##
lib = env.SharedLibrary3('nidas' , [sources, aobj, sampleobj],
                         LIBS = env['LIBS'] + env.NidasUtilLibs() + ['dl'])

# Test SConscripts need the full path to the library to set LD_LIBRARY_PATH
Export({'LIBNIDAS' + arch: lib[0]})

##
## Install targets for libraries and headers.
##
nodes = []
nodes.extend(env.SharedLibrary3Install('$PREFIX',lib))
nodes.extend(env.Install('$PREFIX/include/nidas/core',headers))

Alias('install', nodes)
Clean('install', nodes)

