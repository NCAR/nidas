# -*- python -*-

from SCons.Script import Environment

env = Environment(tools=['default', 'nidasapps', 'valgrind',
                         'boost_test', 'boost_regex'])

tests = env.Program('tgps', ["tgps.cc"])

runtest = env.Command("xtest", tests,
                      env.ChdirActions(["./$SOURCE.file -r detailed -l all "
                                        "--no_color_output"]))
env.Precious(runtest)
env.AlwaysBuild(runtest)
env.Alias('test', runtest)

env.ValgrindLog('memcheck',
                env.Command('vg.memcheck.log', tests,
                            "cd ${SOURCE.dir} && "
                            "${VALGRIND_PATH} --leak-check=full"
                            " --gen-suppressions=all ./${SOURCE.file}"
                            " >& ${TARGET.abspath}"))

env.ValgrindLog('threadcheck',
                env.Command('vg.threadcheck.log', tests,
                            "cd ${SOURCE.dir} && "
                            "${VALGRIND_PATH} --tool=helgrind"
                            " --gen-suppressions=all ./${SOURCE.file}"
                            " >& ${TARGET.abspath}"))
