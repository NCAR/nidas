# -*- python -*-

Import('env')
env = env.Clone(tools = ['nidas', 'valgrind'])

env.Append(CXXFLAGS='-Wno-effc++')
env.Append(LIBS = env.NidasLibs())
env.Append(LIBS = ['boost_unit_test_framework', 'boost_regex'])
env.Prepend(CPPPATH = [ "#/nidas/util", "#/nidas/core" ])
tests = env.Program('tcore', ["tcore.cc", "tutil.cc", "ttimetable.cc"])
# env.Depends(tests, libs)
#

env.AppendENVPath('LD_LIBRARY_PATH', '/opt/nc_server/lib')
runtest = env.Command("xtest", tests, ["$SOURCE.abspath --log_level=all"])
env.Precious(runtest)
AlwaysBuild(runtest)
Alias('test', runtest)

env.ValgrindLog('memcheck', 
                env.Command('vg.memcheck.log', tests,
                            "cd ${SOURCE.dir} && "
                            "${VALGRIND_PATH} --leak-check=full"
                            " --gen-suppressions=all ${SOURCE.file}"
                            " >& ${TARGET.abspath}"))

env.ValgrindLog('threadcheck', 
                env.Command('vg.threadcheck.log', tests,
                            "cd ${SOURCE.dir} && "
                            "${VALGRIND_PATH} --tool=helgrind"
                            " --gen-suppressions=all ${SOURCE.file}"
                            " >& ${TARGET.abspath}"))

schema = env.File("#/xml/nidas.xsd")
xml = env.File("timetable_test.xml")
xmllint = env.Command("xmllint", [schema, xml],
                      ["xmllint --noout --schema ${SOURCES[0].abspath} "
                       "${SOURCES[1]}"])
AlwaysBuild(xmllint)
Alias('test', xmllint)


def write_env(env, target, source):
    "Write a sh file with env settings which can be sourced to run tests."
    out = open(str(target[0]), "w")
    for s in source:
        out.write("export LD_LIBRARY_PATH=%s:${LD_LIBRARY_PATH}\n" %
                  (str(s.dir.get_abspath())))
    out.close()

env.Command('libs.sh', ["$LIBNIDAS", "$LIBNIDAS_UTIL"], write_env)


