# -*- python -*-

from SCons.Script import Environment

env = Environment(tools=['default', 'nidasapps', 'valgrind',
                         'boost_test', 'boost_regex'])

tests = env.Program('tcore', ["tcore.cc", "tsamples.cc",
                              "tutil.cc", "tcalfile.cc",
                              "tdom.cc", "tbadsamplefilter.cc"])

cmd = "echo $$LD_LIBRARY_PATH && ./$SOURCE.file"
runtest = env.Command("xtest", tests, env.ChdirActions([cmd]))
env.Precious(runtest)
env.AlwaysBuild(runtest)
env.Alias('test', runtest)

env.ValgrindLog('memcheck',
                env.Command('vg.memcheck.log', tests,
                            "cd ${SOURCE.dir} && "
                            "${VALGRIND_PATH} --leak-check=full"
                            " --gen-suppressions=all ./${SOURCE.file}"
                            " >& ${TARGET.abspath}"))

env.ValgrindLog('threadcheck',
                env.Command('vg.threadcheck.log', tests,
                            "cd ${SOURCE.dir} && "
                            "${VALGRIND_PATH} --tool=helgrind"
                            " --gen-suppressions=all ./${SOURCE.file}"
                            " >& ${TARGET.abspath}"))
