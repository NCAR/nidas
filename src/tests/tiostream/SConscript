# -*- python -*-

Import(['env','LIBNIDAS_UTIL','LIBNIDAS','LIBNIDAS_DYNLD'])
env = env.Clone()
libs = [LIBNIDAS_UTIL,LIBNIDAS,LIBNIDAS_DYNLD]

ldlibdirs = [ l.Dir('').abspath for l in env.File(libs) ]
env['ENV']['LD_LIBRARY_PATH'] = ":".join(ldlibdirs)

env.Prepend( LIBS= libs, LIBPATH=ldlibdirs)

env.Append(LIBS = ['boost_unit_test_framework', 'boost_regex'])
env.Prepend(CPPPATH = [ '#/src' ])
env.Append(CCFLAGS = ['-Wall'])

tests = env.Program('tiostream', "tiostream.cc")
# env.Depends(tests, libs)

# Add --log_level=all to see all the log messages
runtest = env.Command("xtest", tests, ["$SOURCE.abspath"])
env.Precious(runtest)
AlwaysBuild(runtest)
Alias('test', runtest)



