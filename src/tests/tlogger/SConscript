# -*- python -*-
# 2007, Copyright University Corporation for Atmospheric Research

from SCons.Script import Environment

env = Environment(tools=['default', 'nidasapps', 'valgrind', 'boost_test',
                         'boost_regex'])

tests = env.Program('tlogger', "tlogger.cc")

runtest = env.Command("xtest", tests, ["$SOURCE.abspath"])
env.Precious(runtest)
env.AlwaysBuild(runtest)
env.Alias('test', runtest)

check = env.Command('vg.memcheck.log', tests,
                    "cd ${SOURCE.dir} && "
                    "${VALGRIND_PATH} --leak-check=full"
                    " --gen-suppressions=all ./${SOURCE.file}"
                    " >& ${TARGET.abspath}")
env.AlwaysBuild(check)
env.ValgrindLog('memcheck', check)

check = env.Command('vg.threadcheck.log', tests,
                    "cd ${SOURCE.dir} && "
                    "${VALGRIND_PATH} --tool=helgrind"
                    " --gen-suppressions=all ./${SOURCE.file}"
                    " >& ${TARGET.abspath}")
env.AlwaysBuild(check)
env.ValgrindLog('threadcheck', check)
