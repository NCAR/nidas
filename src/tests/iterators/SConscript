# -*- python -*-

Import('env')
env = env.Clone()

# Set LD_LIBRARY_PATH to the locally build libraries.
# 07/28/2010: env.GetGlobalTarget is not finding these names
# libs = [ env.GetGlobalTarget(name) for name in
#          ['libnidas', 'libnidas_util', 'libnidas_dynld'] ]
libs = [env['LIBNIDAS_UTIL'], env['LIBNIDAS'], env['LIBNIDAS_DYNLD']]
ldlibdirs = [ l.Dir('').abspath for l in env.File(libs) ]
env['ENV']['LD_LIBRARY_PATH'] = ":".join(ldlibdirs)

# We set LIBS to 'nidas_util',... rather than env['LIBNIDAS_UTIL'],...
# because the latter puts the relative path to the library in
# the executable, which is then not runnable from the shell
# script which does a cd to $SOURCE.dir before running the executable.
libs = ['nidas_util','nidas','nidas_dynld']

env.Prepend( LIBS=libs, LIBPATH=ldlibdirs)
env.Append( LIBS=['xerces-c'])

env.Prepend(CPPPATH = [ '#/src' ])
env.Append(CCFLAGS = ['-Wall'])

tests = env.Program('ck_iterators', "ck_iterators.cc")

# runtest = env.Command("ck_itrs",tests,["valgrind $SOURCE.abspath ${SOURCE.dir}/iterator_test.xml"])
runtest = env.Command("ck_itrs",[tests,"run_test.sh"],["cd ${SOURCE.dir} && ./run_test.sh"])
env.Precious(runtest)
AlwaysBuild(runtest)
Alias('ck_itrs', runtest)
Alias('test', runtest)

