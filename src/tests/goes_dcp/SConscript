# -*- python -*-
#
Import('env')
env = env.Clone()
arch = env['ARCH']  # empty string for native builds

Import(['LIBNIDAS_UTIL' + arch,'LIBNIDAS' + arch,'LIBNIDAS_DYNLD' + arch,'NIDAS_APPS' + arch])
libutil = locals()['LIBNIDAS_UTIL' + arch]
libnidas = locals()['LIBNIDAS' + arch]
libdynld = locals()['LIBNIDAS_DYNLD' + arch]
apps = locals()['NIDAS_APPS' + arch]

libs = [libutil,libnidas,libdynld]

# Set LD_LIBRARY_PATH to the locally build libraries.
ldlibdirs = [ l.Dir('').abspath for l in env.File(libs) ]
env['ENV']['LD_LIBRARY_PATH'] = ":".join(ldlibdirs)

pdecode = apps['pdecode']

path = env.subst("${TARGET.dir}", target=pdecode)
env.PrependENVPath('PATH', path)
# print 'path=' + path
# env.PrependENVPath('PATH', env.Dir('#/build_x86/build_apps').abspath)

depends = ["run_test.sh",  pdecode]
runtest = env.Command("goes_dcp", depends, ["cd $SOURCE.dir && ./run_test.sh"])

env.Precious(runtest)
env.AlwaysBuild(runtest)
env.Alias('test', runtest)
env.Alias('goes_dcp_test', runtest)
