# -*- python -*-

Import('env')
env = env.Clone()

# Set LD_LIBRARY_PATH to the locally build libraries.
#
# 07/28/2010: env.GetGlobalTarget is not finding these names
# libs = env.GetGlobalTarget('libnidas_util')
libs = [env['LIBNIDAS_UTIL']]

ldlibdirs = [ l.Dir('').abspath for l in env.File(libs) ]

env['ENV']['LD_LIBRARY_PATH'] = ":".join(ldlibdirs)

# We set LIBS to 'nidas_util' rather than env['LIBNIDAS_UTIL']
# because the latter puts the relative path to the library in
# the executable, which is then not runnable from the shell
# script which does a cd to $SOURCE.dir before running the executable.
env['LIBS']= ['nidas_util']
env['LIBPATH']=ldlibdirs
env.Prepend(CPPPATH = [ '#/src' ])
env.Append(CCFLAGS = ['-Wall'])

tests = env.Program('ck_utime', "ck_utime.cc")


runtest = env.Command("ck_ut",[tests,"run_test.sh"],["cd ${SOURCE.dir} && ./run_test.sh"])
env.Precious(runtest)
AlwaysBuild(runtest)
Alias('ck_utime', runtest)
Alias('test', runtest)

