# -*- python -*-
# 2009-2024, Copyright University Corporation for Atmospheric Research

from SCons.Script import Import, Environment

env: Environment

Import('env')
env = env.Clone()
env.Require('nidas')

dsm = env.NidasApp('dsm')
dsm_server = env.NidasApp('dsm_server')
data_stats = env.NidasApp('data_stats')
sensor_sim = env.NidasApp('sensor_sim')
nidas_setup = '$VARIANT_DIR/bin/nidas_setup.sh'
env.NidasRuntimeENV()

# since test_serial_dsm_server does everything test_serial_dsm does, plus the
# server, it makes sense to just run the one test.  the other test can be run
# separately if that's more convenient for debugging.
depends = ["run_test.sh", dsm, dsm_server, data_stats, sensor_sim]
runtest = env.Command("xtest", depends,
                      ["cd $SOURCE.dir && "
                       "./run_test.sh test_serial_dsm_server"])

env.Precious(runtest)
env.AlwaysBuild(runtest)
env.Alias('test', runtest)
env.Alias('sstest', runtest)
